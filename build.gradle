/*
 * Copyright 2018 Benjamin Bader.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${KOTLIN_VERSION}"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.17"
    }
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jetbrains.dokka'

    group POM_GROUP
    version POM_VERSION

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
}

subprojects {
    project.ext {
        junit4Version = '4.12'
        junit5Version = '5.3.1'

        libs = [
                influxClient: [ "org.influxdb:influxdb-java:2.14" ],
                kotlin: [ "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$KOTLIN_VERSION" ],
                kotlinTest: [
                        "io.kotlintest:kotlintest-assertions:3.1.10",
                        "org.jetbrains.kotlin:kotlin-reflect:$KOTLIN_VERSION",
                ],
                junit4: [ "junit:junit:$junit4Version" ],
                junit5: [
                        "org.junit:junit-bom:$junit5Version"
                ],
                junit5Api: [
                        "org.junit.jupiter:junit-jupiter-api:$junit5Version"
                ]
        ]
    }

    apply from: "$rootDir/gradle/gradle-mvn-publish.gradle"
}